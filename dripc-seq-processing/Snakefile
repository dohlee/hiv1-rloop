import pandas as pd

from pathlib import Path
from os.path import splitext

# Paths
fastq_dir = Path('/data2/project/dohoon/rloop/data/dripc_seq_pooled')
result_dir = Path('result')

# Target sample names.
samples, = glob_wildcards(str(fastq_dir / '{sample}.read1.fastq.gz'))

meta = pd.read_csv('experiment_table.csv')
names_rep12_pooled = list(set(meta.name.values)) # 27 samples.

# (name, replicate) -> (control_sample, treatment_sample)
namerep2ct = {(r.name, r.rep):(r.control1, r.control2, r.treat) for r in meta.to_records()}

assert len(names_rep12_pooled) == 27

qc = expand(str(fastq_dir / '{sample}.read{read}_fastqc.zip'), sample=samples, read=[1, 2])
trimmed = expand(str(result_dir / '01_trim_galore' / '{sample}.read1.trimmed.fastq.gz'), sample=samples)
trimmed_qc = expand(str(result_dir / '01_trim_galore' / '{sample}.read{read}.trimmed_fastqc.zip'), sample=samples, read=[1, 2])
bwa = expand(str(result_dir / '02_bwa' / '{sample}.sorted.bam.bai'), sample=samples)
macs2_filterdup = expand(str(result_dir / '03_macs2' / '{sample}.sorted.filterdup.bed'), sample=samples)

# Narrow/Broad/Merged peaks for each replicate.
narrow_rep = expand(str(result_dir / '04_macs2_callpeak' / 'narrow_rep{i}' / '{name}_peaks.narrowPeak'), name=names_rep12_pooled, i=[1, 2])
broad_rep = expand(str(result_dir / '04_macs2_callpeak' / 'broad_rep{i}' / '{name}_peaks.broadPeak'), name=names_rep12_pooled, i=[1, 2])
merged_rep = expand(str(result_dir / '04_macs2_callpeak' / 'merged_rep{i}' / '{name}_merged_peaks.bed'), name=names_rep12_pooled, i=[1, 2])

signal_track_rep = expand(str(result_dir / '05_macs2_bdg' / 'narrow_rep{i}' / '{name}_ppois.bdg.tdf'), name=names_rep12_pooled, i=[1, 2])

# Narrow/Broad/Merged peaks for pooled peak calling.
narrow_pooled = expand(str(result_dir / '04_macs2_callpeak' / 'narrow_pooled' / '{name}_peaks.narrowPeak'), name=names_rep12_pooled)
broad_pooled = expand(str(result_dir / '04_macs2_callpeak' / 'broad_pooled' / '{name}_peaks.broadPeak'), name=names_rep12_pooled)
merged_pooled = expand(str(result_dir / '04_macs2_callpeak' / 'merged_pooled' / '{name}_merged_peaks.bed'), name=names_rep12_pooled)

signal_track_pooled = expand(str(result_dir / '05_macs2_bdg' / 'narrow_pooled' / '{name}_ppois.bdg.tdf'), name=names_rep12_pooled, i=[1, 2])

include: 'rules/macs2.smk'

ALL = []
ALL.append(qc)
ALL.append(trimmed)
ALL.append(trimmed_qc)
ALL.append(bwa)
ALL.append(macs2_filterdup)

ALL.append(narrow_rep)
ALL.append(broad_rep)
ALL.append(merged_rep)
ALL.append(signal_track_rep)

ALL.append(narrow_pooled)
ALL.append(broad_pooled)
ALL.append(merged_pooled)
ALL.append(signal_track_pooled)

rule all:
    input: ALL

rule fastqc:
    input:
        fastq_dir / '{sample}.read{read}.fastq.gz'
    output:
        html = fastq_dir / '{sample}.read{read,[12]}_fastqc.html',
        zip = fastq_dir / '{sample}.read{read,[12]}_fastqc.zip',
    threads: 2
    wrapper:
        'http://dohlee-bio.info:9193/fastqc'

rule fastqc_after_trimming:
    input:
        result_dir / '01_trim_galore' / '{sample}.read{read}.trimmed.fastq.gz'
    output:
        html = result_dir / '01_trim_galore' / '{sample}.read{read,[12]}.trimmed_fastqc.html',
        zip = result_dir / '01_trim_galore' / '{sample}.read{read,[12]}.trimmed_fastqc.zip',
    threads: 2
    wrapper:
        'http://dohlee-bio.info:9193/fastqc'

rule trim_galore_pe:
    input:
        fastq_dir / '{sample}.read1.fastq.gz',
        fastq_dir / '{sample}.read2.fastq.gz',
    output:
        result_dir / '01_trim_galore' / '{sample}.read1.trimmed.fastq.gz',
        result_dir / '01_trim_galore' / '{sample}.read2.trimmed.fastq.gz',
    params:
        extra = '',
        # Trim low-quality ends from reads in addition to adapter removal. For RRBS samples,
        # quality trimming will be performed first, and adapter trimming is carried in a second round.
        # Other files are quality and adapter trimmed in a single pass. The algorithm is the same
        # as the one used by BWA (Subtract INT from all qualities; compute partial sums from all
        # indices to the end of the sequence; cut sequence at the index at which the sum is minimal).
        # Default: 20
        quality = 20,
        # Instructs Cutadapt to use ASCII+33 quality scores as Phred scores (Sanger/Illumina 1.9+ encoding)
        # for quality trimming.
        # Default: True
        phred33 = True,
        # Instructs Cutadapt to use ASCII+64 quality scores as Phred scores (Illumina 1.5 encoding) for
        # quality trimming.
        # Default: False
        phred64 = False,
        # Run FastQC in the default mode on the FastQ file once trimming is complete.
        # Default: False
        fastqc = False,
        # Passes extra arguments to FastQC. If more than one argument is to be passed to FastQC they must
        # be in the form "arg1 arg2 etc.". An example would be: --fastqc_args "--nogroup --outdir /home/".
        # Passing extra arguments will automatically invoke FastQC, so --fastqc does not have to be specified
        # separately.
        # Default: False
        fastq_args = False,
        # Adapter sequence to be trimmed. If no specified explicitly, Trim Galore will try to auto-detect
        # whether the Illumina universal, Nextera transposase or Illumina small RNA adapter sequence was used.
        # Also see '--illumina', '--nextera' and '--small_rna'. If no adapter can be detected within the
        # first 1 million sequences of the first file specified Trim Galore defaults to '--illumina'.
        # A single base may also be given as e.g. -a A{10}, to be expanded to -a AAAAAAAAAA.
        # Default: False
        adapter = False,
        # Optional adapter sequence to be trimmed off read 2 of paired-end files. This option requires
        # '--paired' to be specified as well. If the libraries to be trimmed are smallRNA then a2 will
        # be set to the Illumina small RNA 5' adapter automatically (GATCGTCGGACT). A single base may
        # also be given as e.g. -a2 A{10}, to be expanded to -a2 AAAAAAAAAA.
        # Default: False
        adapter2 = False,
        # Adapter sequence to be trimmed is the first 13bp of the Illumina universal adapter 'AGATCGGGAGC'
        # instead of the default auto-detection of adapter sequence.
        # Default: False
        illumina = False,
        # Adapter sequence to be trimmed is the first 12bp of the Nextera adapter 'CTGTCTCTTATA' instead of
        # default auto-detection of adapter sequence.
        # Default: False
        nextera = False,
        # Adapter sequence to be trimmed is the first 12bp of the Illumina Small RNA 3' Adapter
        # 'TGGTTCTCGG' instead of the default auto-detection of adapter sequence. Selecting to trim
        # smallRNA adapters will also lower the --length value to 18bp. If the smallRNA libraries
        # are paired-end then a2 will be set to the Illumina small RNA 5' adapter automatically
        # (GATCGTCGGACT) unless -a 2 had been defined explicitly.
        # Default: False
        small_rna = False,
        # Discard reads that are longer than <INT> bp after trimming. This is only advised for smallRNA
        # sequencing to remove non-small RNA sequences.
        # Default: False
        max_length = False,
        # Overlap with adapter sequence required to trim a sequence. Defaults to a very stringent setting of
        # 1, i.e. even a single bp of overlapping sequence will be trimed off from the 3' end of any read.
        # Default: 1
        stringency = 1,
        # Maximum allowed error rate (no. of errors devided by the length of the matching region)
        # Default: 0.1
        e = 0.1,
        # Discard reads that became shorter than length INT because of either quality or adapter trimming.
        # A value of '0' effectively disables this behaviour.
        # For paired-end files, both reads of a read-pair need to be longer than <INT> bp to be printed out
        # to validated paired-end files (see option --paired).
        # If only one read became too short there is the possibility of keeping such unpaired single-end reads
        # (see --retain_unpaired).
        # Default: 20
        length = 20,
        # The total number of Ns (as integer) a read may contain before it will be removed altogether.
        # In a paired-end setting, either read exceeding this limit will result in the entire pair
        # being removed from the trimmed output files.
        # Default: False
        max_n = False,
        # Removes Ns from either side of the read. This option does currently not work in RRBS mode.
        # Default: False
        trim_n = False,
        # If specified no report file will be generated.
        # Default: False
        no_report_file = False,
        # If specified any output to STDOUT or STDERR will be wuppressed.
        # Default: False
        suppress_warn = False,
        # Instructs Trim Galore to remove <int> bp from the 5' end of read 1 (or single-end reads).
        # This may be useful if the qualities were very poor, or if there is some sort of unwanted bias at
        # the 5' end.
        # Default: False
        clip_R1 = False,
        # Instructs Trim Galore to remove <int> bp from the 5' end of read 2 (paired-end reads only).
        # This may be useful if the qualities were very poor, or if there is some sort of unwanted bias at
        # the 5' end. For paired-end BS-Seq, it is recommended to remove the first fewe bp because the end-repair
        # reaction may introduce a bias towards low methylation. Please refer to the M-bias plot section in the
        # Bismark User Guide for some examples.
        # Default: False
        clip_R2 = False,
        # Instructs Trim Galore to remove <int> bp from the 3' end of read 1 (or single-end reads) AFTER
        # adapter/quality trimming has been performed. This may remove some unwanted bias from the 3' end
        # that is not directly related to adapter sequence or basecall quality.
        # Default: False
        three_prime_clip_R1 = False,
        # Instructs Trim Galore to remove <int> bp from the 3' end of read 2 AFTER adapter/quality trimming
        # has been performed. This may remove some unwanted bias from the 3' end that is not directly related
        # to adapter sequence or basecall quality.
        # Default: False
        three_prime_clip_R2 = False,
        # This enables the option '--nextseq-trim=3'CUTOFF' within Cutadapt, which will set a quality cutoff
        # (that is normally given with -q instead), but qualities of G bases are ignored.
        # This trimming is in common for the NextSeq- and NovaSeq-platforms, where basecalls without any
        # signal are called as high-quality G bases. This is mutually exclusive with '-q INT'.
        nextseq = False,
        # Use PREFERRED_NAME as the basename for output files, instead of deriving the filenames from the
        # input files. Single-end data would be called PREFERRED_NAME_trimmed.fa(.gz), or
        # PREFERRED_NAME_val_1.fq(.gz) and PREFERRED_NAME_val_2.fq(.gz) for paired-end data.
        # --basename only works when 1 file (single-end) or 2 files (paired-end) are specified, but nor for
        # longer lists.
        # Default: False
        basename = False,
        # 
        # RRBS-specific options (MspI digested material)
        # 
        # Specifies that the input file was an MspI digested RRBS sample (recognition site: CCGG).
        # Single-end or Read 1 sequences (paired-end) which were adapter-trimmed will have a further 2 bp 
        # removed from their 3' end. Sequences which were merely trimmed because of poor quality will not
        # be shortened further. Read 2 of paired-end libraries will in addition have the first 2 bp removed
        # from the 5' end (by setting '--clip_r2 2'). This is to avoid using artificial methylation calls from
        # the filled-in cytosine positions close to the 3' MspI site in sequenced fragments. This otion is
        # not recommended for users of the NuGEN ovation RRBS System 1-16 kit (see below).
        # Default: False
        rrbs = False,
        # Selecting this option for non-directional RRBS libraries will screen quality-trimmed sequences for
        # 'CAA' or 'CGA' at the start of the read and, if found, removes the first bwo basepairs. Like with
        # the option '--rrbs' this avoids using cytosine positions that were filled-in during the end-repair
        # step. '--non_directional' requires '--rrbs' to be specified as well. Note that this option does not
        # set '--clip_r2 2' in paired-end mode.
        # Default: False
        non_directional = False,
        # Keep the quality trimmed intermediate file, which means the temporary file is being deleted after
        # adapter trimming. Only has an effect for RRBS samples since other FastQ files are not trimmed for
        # poor qualities separately.
        # Default: False
        keep = False,
        #
        # Paired-end specific options
        #
        # This option performs length trimmng of quality/adapter/RRBS trimmed reads for paired-end files. To
        # pass the validation test, both sequences of a sequence pair are required to have a certain minimum
        # length which is governed by the option --length (see above). If only one read passes this length
        # threshold the other read cen be rescued (see option --retain_unpaired). Using this option lets 
        # you discard too short read pairs without disturbint the sequence-by-sequence order of FastQ files
        # which is required by many aligners.
        # Trim Galore! expects paired-end files to be supplied in a pairwise fashion, e.g. file1_1.fq
        # file1_2.fq, SRR2_1.fa.gz SRR2_2.fq.gz, ...
        # Default: False
        paired = False,
        # Trims 1 bp off every read from its 3' end. This may be needed for FastQ files that are to be
        # aligned as paired-end data with Bowtie. This is because Bowtie regards alignments like this:
        #
        # R1 -------> or this:  ---------> R1
        # R2 <-------              <------ R2 
        # 
        # as invalid (whenever a start/end coordinate is contained within the other read).
        # NOTE: If you are planning to use Bowtie2, BWA etc. you don't need to specify this option.
        # Default: False
        trim1 = False,
        # If only one of the two paired-end reads became too short, the longer read will be written to
        # either '.unpaired_1.fq' or '.unpaired_2.fq' output files. The length cutoff for unpaired single-end
        # reads is governed by the parameters -r1/--length_1 and -r2/--length_2.
        # Default: False
        retain_unpaired = False,
        # Unpaired single-end read length cutoff needed for read 1 to be written to '.unpaired_1.fq' output
        # file. These reads may be mapped in single-end mode.
        # Default: 35
        length_1 = False,
        # Unpaired single-end read length cutoff needed for read 2 to be written to '.unpaired_2.fq' output
        # file. These reads may be mapped in single-end mode.
        # Default: 35
        length_2 = False,
    threads: 4
    resources: io=1
    log: 'logs/trim_galore/{sample}.log'
    benchmark: 'benchmarks/trim_galore/{sample}.log'
    wrapper: 'http://dohlee-bio.info:9193/trim-galore'

rule bwa_mem:
    input:
        # Required input. Input read file.
        reads = [
            result_dir / '01_trim_galore' / '{sample}.read1.trimmed.fastq.gz',
            result_dir / '01_trim_galore' / '{sample}.read2.trimmed.fastq.gz',
        ],
        # You may use any of {genome}.amb, {genome}.ann, {genome}.bwt,
        # {genome}.pac, {genome}.sa just to obligate snakemake to run `bwa index` first.
        reference = '/data/project/dohoon/reference/hg38/bwa_index/hg38.bwt'
    output:
        # BAM output or SAM output is both allowed.
        # Note that BAM output will be automatically detected by its file extension,
        # and SAM output (which is bwa mem default) will be piped through `samtools view`
        # to convert SAM to BAM.
        result_dir / '02_bwa' / '{sample}.bam'
    params:
        extra = '',
        # Minimum seed length.
        # Default: 19
        k = 19,
        # Band width for banded alignment.
        # Default: 100
        w = 100,
        # Off-diagonal X-dropoff.
        # Default: 100
        d = 100,
        # Look for internal seeds inside a seed longer than {-k} * FLOAT
        # Default: 1.5
        r = 1.5,
        # Seed occurrence for the 3rd round seeding.
        # Default: 20
        y = 20,
        # Skip seeds with more than INT occurrences.
        # Default: 500
        c = 500,
        # Drop chains shorter than FLOAT fraction of the logest overlapping chain.
        # Default: 0.5
        D = 0.50,
        # Discard a chain if seeded bases shorter than INT.
        # Default: 0
        W = 0,
        # Perform at most INT rounds of mate rescues for each read.
        # Default: 50
        m = 50,
        # Skip mate rescue.
        # Default: False
        S = False,
        # Skip pairing; mate rescue performed unless -S also in use
        # Default: False
        P = False,
        # Score for a sequence match, which scales options -TdBOELU unless overridden.
        # Default: 1
        A = 1,
        # Penalty for a mismatch.
        # Default: 4
        B = 4,
        # Gap open penalties for deletions and insertions.
        # Default: 6,6
        O = '6,6',
        # Gap extension penalty; a gap of size k cost '{-O} + {-E}*k'.
        # Default: 1,1
        E = '1,1',
        # Penalty for 5'- and 3'-end clipping.
        # Default: 5,5
        L = '5,5',
        # Penalty for an unpaired read pair.
        # Default: 17
        U = 17,
        # Read type. Setting -x changes multiple parameters unless overridden [null]
        # pacbio: -k17 -W40 -r10 -A1 -B1 -O1 -E1 -L0 (PacBio reads to ref)
        # ont2d: -k14 -W20 -r10 -A1 -B1 -O1 -E1 -L0 (Oxford Nanopore 2D-reads to ref)
        # intractg: -B9 -O16 -L5 (intra-species contigs to ref)
        # Default: False
        x = False,
        # Read group header line such as '@RG\tID:foo\tSM:bar'
        # Default: False
        # NOTE: You should check the platform information of the read data!
        R = r"'@RG\tID:{sample}\tSM:{sample}\tPL:ILLUMINA'",
        # Insert STR to header if it starts with @; or insert lines in FILE.
        # Default: False
        H = False,
        # Treat ALT contigs as part of the primary assembly. (i.e. ignore <idxbase>.alt file)
        # Default: False
        j = False,
        # For split alignment, take the alignment with the smallest coordinate as primary.
        # Default: False
        _5 = False,
        # Dont't modify mapQ of supplementary alignments.
        # Default: False
        q = False,
        # Process INT input bases in each batch regardless of nThreads (for reproducibility).
        # Default: False.
        K = False,
        # Verbosity level: 1=error, 2=warning, 3=message, 4+=debugging
        # Default: 3
        v = 3,
        # Minimum score to output.
        # Default: 30
        T = 30,
        # If there are <INT hits with score > 80% of the max score, output all in XA.
        # Default: 5,200
        h = '5,200',
        # Output all alignments for SE or unpaired PE.
        # Default: False
        a = False,
        # Append FASTA/FASTQ comment to SAM output.
        # Default: False
        C = False,
        # Output the reference FASTA header in the XR tag.
        # Default: False
        V = False,
        # Use soft clipping for supplementary alignments.
        # Default: False
        Y = False,
        # Mark shorter split hits as secondary.
        # NOTE: You may need this if you use GATK downstream.
        # Default: False
        M = False,
        # Specify the mean, standard deviation (10% of the mean if absent), max
        # (4 sigma from the mean if absent) and min of the insert size distribution.
        # FR orientation only.
        # Default: False (inferred)
        I = False,
    threads: 8
    log: 'logs/bwa_mem/{sample}.log'
    benchmark: 'benchmarks/bwa_mem/{sample}.benchmark'
    wrapper:
        'http://dohlee-bio.info:9193/bwa/mem'

rule sambamba_sort:
    input:
        result_dir / '02_bwa' / '{sample}.bam'
    output:
        result_dir / '02_bwa' / '{sample}.sorted.bam'
    params:
        extra = '',
        # Sets an upper bound for used memory. However, this is very approximate.
        # Default memory limit is 512 MiB. Increasing it will allow to make chunk
        # sizes larger and also reduce amount of I/O seeks thus improving the
        # overall performance. LIMIT must be a number with an optional suffix
        # specifying unit of measurement.
        # The following endings are recongnized: K, KiB, KB, M, MiB, MB, G, GiB, GB
        # Default: False
        memory_limit = False,
        # Use TMPDIR to output sorted chunks. Default behavior is to use system 
        # temporary directory.
        # Default: False
        tmpdir = '.',
        # Sort by read name instead of doing coordinate sort.
        # Default: False
        sort_by_name = False,
        # Compression level to use for sorted BAM, from 0 (known as uncompressed
        # BAM in samtools) to 9.
        # Default: False
        compression_level = False,
        # Write sorted chuks as uncompressed BAM. Default dehaviour is to write them
        # with compression level 1, because that reduces time spent on I/O, but in
        # some cases using this option can give you a better speed. Note, however,
        # that the disk space needed for sorgin will typically be 3-4 times more
        # than without enabling this option.
        # Default: False
        uncompressed_chunks = False,
        # Show wget-like progressbar in STDERR (in fact, two of them one after another,
        # first one for sorting, and then another one for merging.)
        # Default: False
        show_progress = False,
    threads: 1
    log: 'logs/sambamba_sort/{sample}.log'
    benchmark: 'benchmarks/sambamba_sort/{sample}.benchmark'
    wrapper:
        'http://dohlee-bio.info:9193/sambamba/sort'

rule sambamba_index:
    input:
        result_dir / '02_bwa' / '{sample}.sorted.bam'
    output:
        result_dir / '02_bwa' / '{sample}.sorted.bam.bai'
    threads: 1
    log: 'logs/sambamba_index/{sample}.log'
    benchmark: 'benchmarks/sambamba_index/{sample}.benchmark'
    wrapper:
        'http://dohlee-bio.info:9193/sambamba/index'

rule to_tdf_for_replicate:
    input:
        result_dir / '05_macs2_bdg' / 'narrow_rep{i}' / '{sample}_ppois.bdg'
    output:
        result_dir / '05_macs2_bdg' / 'narrow_rep{i}' / '{sample}_ppois.bdg.tdf'
    params:
        genome_version = 'hg38'
    threads: 1
    log: 'logs/igvtools/to_tdf/{sample}_rep{i}.log'
    benchmark: repeat('benchmarks/igvtools/to_tdf/{sample}_rep{i}.benchmark', 1)
    wrapper:
        'http://dohlee-bio.info:9193/igvtools/totdf'

rule to_tdf_for_pooled:
    input:
        result_dir / '05_macs2_bdg' / 'narrow_pooled' / '{sample}_ppois.bdg'
    output:
        result_dir / '05_macs2_bdg' / 'narrow_pooled' / '{sample}_ppois.bdg.tdf'
    params:
        genome_version = 'hg38'
    threads: 1
    log: 'logs/igvtools/to_tdf/{sample}.log'
    benchmark: repeat('benchmarks/igvtools/to_tdf/{sample}.benchmark', 1)
    wrapper:
        'http://dohlee-bio.info:9193/igvtools/totdf'

